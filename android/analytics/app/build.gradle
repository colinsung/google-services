import java.text.SimpleDateFormat
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0

def buildTime() {
    def df = new SimpleDateFormat("yyMMddhhmm")
    return df.format(new Date())
}

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.ccc.samples.quickstart.analytics"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode Integer.parseInt(buildTime())
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${buildTime()}"
    }

    if(System.getenv("ANDROID_KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("ANDROID_KEYSTORE"))
                storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
                keyAlias System.getenv("ANDROID_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEY_PASSWORD")
            }
        }

	buildTypes {
            release {
                signingConfig signingConfigs.release
	    }
        }
    }else{
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt')
            }
        }
    }

    lintOptions {
          abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.android.support:support-v4:22.1.0'
    compile 'com.google.android.gms:play-services-analytics:7.0.0'
}
