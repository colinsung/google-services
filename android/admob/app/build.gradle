import java.text.SimpleDateFormat
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0

def buildTime() {
    def df = new SimpleDateFormat("yyMMddhhmm")
    return df.format(new Date())
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.ccc.samples.quickstart.admobexample"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode Integer.parseInt(buildTime())
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${buildTime()}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    if(System.getenv("ANDROID_KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("ANDROID_KEYSTORE"))
                storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
                keyAlias System.getenv("ANDROID_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEY_PASSWORD")
            }
        }

	buildTypes {
            release {
                signingConfig signingConfigs.release
	    }
        }
    }else{

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // AppCompat
    compile 'com.android.support:appcompat-v7:22.1.0'

    // [START gradle_play_config]
    compile 'com.google.android.gms:play-services-base:7.5.0'
    compile 'com.google.android.gms:play-services-ads:7.5.0'
    // [END gradle_play_config]

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.1'
    androidTestCompile 'com.android.support.test:runner:0.2'
}
